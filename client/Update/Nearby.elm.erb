module Update.Nearby exposing (update)

import Task
import Date
import Geolocation exposing (Location)
import Http
import Model.Nearby exposing (..)
import Decoder.Nearby


update : Msg -> Model -> ( Model, Cmd Msg )
update msg model =
    case msg of
        GetLocation _ ->
            ( NoLocation, Task.attempt useLocation Geolocation.now )

        SetLocation location ->
            let
                newModel =
                    ReceivedLocation location.latitude location.longitude

                cmd =
                    Task.succeed location
                        |> Task.perform RequestSchedule
            in
                ( newModel, cmd )

        UnavailableLocation error ->
            let
                message =
                    case error of
                        Geolocation.PermissionDenied value ->
                            value

                        Geolocation.LocationUnavailable value ->
                            value

                        Geolocation.Timeout value ->
                            value
            in
                ( Error message, Cmd.none )

        RequestSchedule location ->
            let
                request =
                    requestSchedule location.latitude location.longitude

                cmd =
                    Http.send ReceiveSchedule request
            in
                ( model, cmd )

        ReceiveSchedule (Ok response) ->
            let
                getTime =
                    Date.now
                        |> Task.perform (ReceiveTime response)
            in
                ( model, getTime )

        ReceiveSchedule (Err error) ->
            let
                message =
                    case error of
                        Http.BadUrl message ->
                            message

                        Http.Timeout ->
                            "Timeout"

                        Http.NetworkError ->
                            "Network error"

                        Http.BadStatus response ->
                            toString response.status.code

                        Http.BadPayload message _ ->
                            message
            in
                ( Error message, Cmd.none )

        ReceiveTime nearby date ->
            ( ReceivedDate nearby date, Cmd.none )

        None ->
            ( model, Cmd.none )


useLocation : Result Geolocation.Error Location -> Msg
useLocation result =
    case result of
        Ok location ->
            SetLocation location

        Err err ->
            UnavailableLocation err


requestSchedule : Float -> Float -> Http.Request Nearby
requestSchedule latitude longitude =
    let
        url =
            "<%= restbus_url %>"

        fullUrl =
            url ++ "?latitude=" ++ (toString latitude) ++ "&longitude=" ++ (toString longitude)
    in
        Http.get fullUrl Decoder.Nearby.nearby
