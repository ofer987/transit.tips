module Update.Nearby exposing (update)

import Task
import Date
import Geolocation exposing (Location)
import Http
import Model.Nearby exposing (..)
import Json.Nearby
import Decoder.Nearby


update : Msg -> Model -> ( Model, Cmd Msg )
update msg model =
    case msg of
        GetLocation _ ->
            ( NoLocation, Task.attempt useLocation Geolocation.now )

        SetLocation location ->
            let
                newModel =
                    ReceivedLocation location.latitude location.longitude

                cmd =
                    Task.succeed location
                        |> Task.perform RequestNearby
            in
                ( newModel, cmd )

        UnavailableLocation error ->
            let
                message =
                    case error of
                        Geolocation.PermissionDenied value ->
                            value

                        Geolocation.LocationUnavailable value ->
                            value

                        Geolocation.Timeout value ->
                            value
            in
                ( Error message, Cmd.none )

        RequestNearby location ->
            let
                request =
                    requestNearby location.latitude location.longitude

                cmd =
                    Http.send ReceiveNearby request
            in
                ( model, cmd )

        ReceiveNearby (Ok response) ->
            let
                getTime =
                    Date.now
                        |> Task.perform (ReceiveTime response)
            in
                ( model, getTime )

        ReceiveNearby (Err error) ->
            let
                message =
                    case error of
                        Http.BadUrl message ->
                            message

                        Http.Timeout ->
                            "Timeout"

                        Http.NetworkError ->
                            "Network error"

                        Http.BadStatus response ->
                            toString response.status.code

                        Http.BadPayload message _ ->
                            message
            in
                ( Error message, Cmd.none )

        ReceiveTime nearby date ->
            ( ReceivedDate nearby date, Cmd.none )

        None ->
            ( model, Cmd.none )


useLocation : Result Geolocation.Error Location -> Msg
useLocation result =
    case result of
        Ok location ->
            SetLocation location

        Err err ->
            UnavailableLocation err


requestNearby : Float -> Float -> Http.Request Json.Nearby.Nearby
requestNearby latitude longitude =
    let
        url =
            "<%= restbus_url %>"

        fullUrl =
            url ++ "?latitude=" ++ (toString latitude) ++ "&longitude=" ++ (toString longitude)
    in
        Decoder.Nearby.nearby
            |> Decoder.Nearby.toModel
            |> Http.get fullUrl
